package app

import (
	"list"
	"strings"

	"github.com/hofstadter-io/hof/schema/gen"

	"github.com/hofstadter-io/supacode/schema"
)

// Generator definition
Generator: gen.Generator & {
	ModuleName: "github.com/hofstadter-io/supacode"

	// User inputs to this generator
	// -----------------------------

	// The server design conforming to the server schema
	Datamodel: schema.Datamodel
	App:       schema.App

	// Base output directory, defaults to current
	Outdir: string | *"./"

	// Required fields for hof
	// ------------------------

	// In is passed to every template
	In: {
		"App":       App
		"Datamodel": Datamodel

		// shorthand
		DM: Datamodel
	}

	gen.TemplateSubdirs & {#subdir: "app"}
	Templates: [{
		DelimGlobs: "**/*.tsx": {LHS: "{%", RHS: "%}"}
	}, ...]

	// Actual files generated by hof, combined into a single list
	Out: [...gen.File] & _All

	_All: [
		for _, F in _OnceFiles {F},
		for _, F in _ModelFiles {F},
	]

	// Note, we can omit Templates, Partials, and Statics
	// since the default values are sufficient for us

	// Internal fields for mapping Input to templates
	// ----------------------------------------------

	// Files that are generated once per server
	_OnceFiles: [...gen.File] & [ {
		TemplatePath: "package.json"
		Filepath:     "package.json"
	}, {
		TemplatePath: "app/globals.css"
		Filepath:     "app/globals.css"
	}, {
		TemplatePath: "app/page.tsx"
		Filepath:     "app/page.tsx"
	}, {
		TemplatePath: "app/navbar.css"
		Filepath:     "app/navbar.css"
	}, {
		TemplatePath: "app/navbar.tsx"
		Filepath:     "app/navbar.tsx"
	}, {
		TemplatePath: "app/layout.tsx"
		Filepath:     "app/layout.tsx"
	}]

	// This is a pattern for defining multiple files per element during interation
	// we will produce a list of lists [models -> files], and need to flatten them
	_ModelFiles: [...gen.File] & list.FlattenN([ for m, M in Datamodel.Models {
		let lm = strings.ToLower(m)

		// apply an enrichment to each item in the file list
		// here we are providing a top-level "Model" value to the template input
		[...{In: Model: M}]// (CUE pattern)

		// the file list we produce for each model
		[{
			TemplatePath: "src/types/model.ts"
			Filepath:     "src/types/\(lm).ts"
		}, {
			TemplatePath: "app/page.tsx"
			Filepath:     "app/\(lm)/page.tsx"
		}, {
			TemplatePath: "app/route.ts"
			Filepath:     "app/api/\(lm)/route.ts"
		}]
	}], 1)
	...
}
